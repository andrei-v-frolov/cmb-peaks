#!/usr/bin/env gnuplot

set terminal postscript eps enhanced solid color
set output "noise.eps"; set size 0.80

lmax=2000; lbeam=1200; alpha=0.75

# beam function for temperature map (squared because we fit Cl's)
B(l)=exp(-l*(l+1)/lbeam**2/2.0)**2; uK = 1.0e-6

# effective beam (10' FWHM Gaussian/Pixel window at n=2048)
BFWHM = 10.0 * pi/(180*60) # 5 arcmin
P2048 = 1.21333461333613 * pi/(180*60)
lbeam = sqrt((8*log(2))/(BFWHM**2-P2048**2))

print "Effective l_beam = ", lbeam/sqrt(2)

# plot covariance model components
set title "{/Times-Roman*1.6 `cat ../DATASET` ad-hoc power model}"
#set logscale; set key bottom right; set format y "%g"
set xlabel 'l'; set ylabel 'C_l l(l+1)/2{/Symbol p}, K^2'
plot [2:lmax] \
     'cl_full_t1.txt' u 0:($1*$0*($0+1)/(2.0*pi)) w l t 'pseudo-C_l from full-sky map', \
     'base_planck_lowl_lowLike_highL_post_lensing.bestfit_cl' u 1:($4*B($1)) w l t 'best-fit CMB EE spectrum C_l', \
     'cl_full_t1.txt' u 0:($1*$0*($0+1)/(2.0*pi)) smooth bezier w l lw 3 t 'total C_l covariance model'
# convert the plot to pdf
! ../bins/epstopdf noise.eps

# dump the tabulated model
set table "noise.dat"; set samples `wc -l < cl_full_t1.txt`; set format y "%.16g"
plot [2:lmax] 'cl_full_t1.txt' u 0:($1*$0*($0+1)/(2.0*pi)) smooth bezier
unset table

# select only valid data
! awk '$3 == "i" {print $1, $2;}' noise.dat > base_planck_bestfit_cmb_adhoc_noise.cl && rm -f noise.dat

