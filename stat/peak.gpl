#!/usr/bin/env gnuplot

set terminal postscript eps enhanced solid color

set size 0.6; set lmargin 5; set rmargin 1

N = `wc -l < @DATA@.dat`; NN=N-1
M = `head -1 @DATA@.dat | awk '{print $2;}'`
P = `head -1 @DATA@.dat | awk '{print $1;}'`

# initial guess for distribution
gamma = 0.75; sigma = abs(M)/4.3
set fit errorvariables; FIT_LIMIT = 1.0e-12

# Kolmogorov-Smirnov confidence levels
Q99 = 0.3989178859; Q95 = 0.5137003373; Q68 = 0.7170542898

mu(nu,gamma) = nu/sqrt(2*(1-2*gamma**2/3))
cdf(nu) = sqrt(1.5/pi) * gamma**2 * nu*exp(-nu**2/2) + erfc(mu(nu,gamma))/2
fit 1.0-cdf(x/sigma-alpha) '@DATA@.dat' using 2:($0/NN) via alpha,gamma,sigma

# significance of the minimum
X = M/sigma-alpha; S = 1.0-cdf(X)**N
Smax = 1.0-cdf(M/(sigma+sigma_err)-alpha+alpha_err)**N
Smin = 1.0-cdf(M/(sigma-sigma_err)-alpha-alpha_err)**N

set output "@DATA@.eps"
set print "@FILTER@.dat" append

set multiplot
set size 0.6,0.417; set origin 0,0
set key left Left reverse
set xrange [-6:6]; set xtics 1.0
set format x '%.0f{/Symbol s}'
set ytics 0.25; set format y '%.2f'

set label "{/Helvetica-Narrow @KERNEL@ filter at @BEAM@ arcmin FWHM}" at 3,0.3 center
set label sprintf("{/Helvetica-Narrow %i peaks, coldest at (%.2f%+.2f){/Symbol s}}", N, alpha, X) at 3,0.2 center
set label sprintf("{/Helvetica-Bold significance 1:%.1f}", 1.0/S) at 3,0.1 center

print sprintf("@BEAM@ %i %g %g %e %e %e %e %i %g %g %g @DATA@", P, M, X, 1.0-cdf(X), S, Smin, Smax, N, sigma, gamma, alpha)

plot '@DATA@.dat' u ($2/sigma):($0/NN) w p lt 1 t '{/Times-Roman @DATA@ data}', 1.0-cdf(x-alpha) w l lt 2 lw 5 t '{/Times-Roman Gaussian peak stats}'

set size 0.6,0.25; set origin 0,0.35
set nokey; unset label; unset xtics
set yrange [-0.99:1.0]; set ytics 0.25
set label "{/Times-Roman Kolmogorov deviation from Gaussian peak CDF}" at 0,1.2 center
plot Q68 with filledcurve y1=-Q68 lc rgb '#FFEEEE', Q95 with filledcurve y1=-Q95 lc rgb '#FFDDDD', Q99 with filledcurve y1=-Q99 lc rgb '#FFCCCC', 0 with lines lt 0, '@DATA@.dat' u ($2/sigma):((sqrt(N)+0.12+0.11/sqrt(N))*($0/NN+cdf($2/sigma-alpha)-1.0)) with points lt 1

set size 0.16,0.23; set origin 0,0.075
set xrange [-6:-4]; set yrange [1e-5:*]
set logscale y; unset ytics; set grid y2
set y2tics 1.0; set y2label 'log_{10} CDF' offset -0.5,0
set x2tics add M/sigma,2,0; set grid x2
set format x2 '%.2f{/Symbol s}'; unset label
plot 1.0-cdf(x-alpha) w l lt 2 lw 5
unset multiplot

# clean up
! ../bins/epstopdf @DATA@.eps
