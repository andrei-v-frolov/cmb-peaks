#!/usr/bin/env gnuplot

set output "sign.eps"
set terminal postscript eps enhanced solid color

set size 1.20,0.8; set lmargin 4; set rmargin 4

! sort -k1g -k11 L1-SSG??.dat | python spot.py > SPOTS

! awk '$13 ~ "SSG02" && $14 == 1 { print; }' < SPOTS > SSG02-A.dat
! awk '$13 ~ "SSG02" && $14 == 2 { print; }' < SPOTS > SSG02-B.dat
! awk '$13 ~ "SSG02" && $14 >= 3 { print; }' < SPOTS > SSG02-C.dat

! awk '$13 ~ "SSG42" && $14 == 1 { print; }' < SPOTS > SSG42-A.dat
! awk '$13 ~ "SSG42" && $14 == 2 { print; }' < SPOTS > SSG42-B.dat
! awk '$13 ~ "SSG42" && $14 >= 3 { print; }' < SPOTS > SSG42-C.dat

! awk '$13 ~ "SSG84" && $14 == 1 { print; }' < SPOTS > SSG84-A.dat
! awk '$13 ~ "SSG84" && $14 == 2 { print; }' < SPOTS > SSG84-B.dat
! awk '$13 ~ "SSG84" && $14 >= 3 { print; }' < SPOTS > SSG84-C.dat

set logscale; set key bottom left Left reverse
set xrange [2:20]
set xlabel "filter kernel size [deg full width half max]" offset 0,0.5
set ytics 0.1 nomirror
set yrange [1e-4:1]; set format y "10^{%T}"
set ylabel "probability of Gaussian realization" offset 0.5
set y2label "effective confidence level [{/Symbol s}]" offset -1,0

set xtics (1,2,3,4,5,6,7,8,9,10,12,14,16,18,20,22,24,26,28,30,32) rotate

set y2tics ( \
    "1.0" erfc(1.0/sqrt(2)), \
    "1.5" erfc(1.5/sqrt(2)), \
    "2.0" erfc(2.0/sqrt(2)), \
    "2.5" erfc(2.5/sqrt(2)), \
    "3.0" erfc(3.0/sqrt(2)), \
    "3.5" erfc(3.5/sqrt(2)), \
    "4.0" erfc(4.0/sqrt(2))  \
)

set grid y2tics
set samples 1053

plot erfc(3.0/sqrt(2)) w filledcurves above x1 lt 0 lc rgb '#FAE7B3' t '', \
     'L1-SSG02.dat' u ($1/60.0):6 smooth csplines w l t '{/Times-Roman SSG02 filter      ({/Symbol L}CDM optimal)}' lw 7 lc rgb '#FFAAAA', \
     'L1-SSG42.dat' u ($1/60.0):6 smooth csplines w l t '{/Times-Roman SSG42 filter      ({/Symbol L}CDM optimal)}' lw 7 lc rgb '#AAFFAA', \
     'L1-SSG84.dat' u ($1/60.0):6 smooth csplines w l t '{/Times-Roman SSG84 filter      ({/Symbol L}CDM optimal)}' lw 7 lc rgb '#AAAAFF', \
     'SSG02-A.dat' u ($1/60.0):6:7:8 w e lt 1 pt 5 t '', \
     'SSG02-B.dat' u ($1/60.0):6:7:8 w e lt 1 pt 6 t '', \
     'SSG02-C.dat' u ($1/60.0):6:7:8 w e lt 1 pt 1 t '', \
     'SSG42-A.dat' u ($1/60.0):6:7:8 w e lt 2 pt 5 t '', \
     'SSG42-B.dat' u ($1/60.0):6:7:8 w e lt 2 pt 6 t '', \
     'SSG42-C.dat' u ($1/60.0):6:7:8 w e lt 2 pt 1 t '', \
     'SSG84-A.dat' u ($1/60.0):6:7:8 w e lt 3 pt 5 t '', \
     'SSG84-B.dat' u ($1/60.0):6:7:8 w e lt 3 pt 6 t '', \
     'SSG84-C.dat' u ($1/60.0):6:7:8 w e lt 3 pt 1 t ''

# clean up
! rm -f SSG??-[A-C].dat; ../bins/epstopdf sign.eps
